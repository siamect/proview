!** Invisible: Script used by reload.sh to save classvolume versions
! 
# Proview   Open Source Process Control.
# Copyright (C) 2005-2012 SSAB EMEA AB.
#
# This file is part of Proview.
!
!  This program is free software; you can redistribute it and/or 
!  modify it under the terms of the GNU General Public License as 
!  published by the Free Software Foundation, either version 2 of 
!  the License, or (at your option) any later version.
!
!  This program is distributed in the hope that it will be useful 
!  but WITHOUT ANY WARRANTY; without even the implied warranty of 
!  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the 
!  GNU General Public License for more details.
!
!  You should have received a copy of the GNU General Public License 
# along with Proview. If not, see <http://www.gnu.org/licenses/>
#
# Linking Proview statically or dynamically with other modules is
# making a combined work based on Proview. Thus, the terms and 
# conditions of the GNU General Public License cover the whole 
# combination.
#
# In addition, as a special exception, the copyright holders of
# Proview give you permission to, from the build function in the
# Proview Configurator, combine Proview with modules generated by the
# Proview PLC Editor to a PLC program, regardless of the license
# terms of these modules. You may copy and distribute the resulting
# combined work under the terms of your choice, provided that every 
# copy of the combined work is accompanied by a complete copy of 
# the source code of Proview (the version used to produce the 
# combined work), being distributed under the terms of the GNU 
# General Public License plus this exception.
!
!

main()
  string v;
  string class;
  string attr;
  int    version;
  int	 sts;
  int	 file;
  string fname;
  string cmd;
  int    file_open;

  printf("\n");
  printf("-- ssab_exe:ssab_reload_vol_versions\n");
  printf("-- Script to store current classvolume versions\n\n");

  fname = "$pwrp_db/reload_vol_versions_" + p1 + ".pwr_com";
  fname = translate_filename( fname);
  file_open = 0;
  v = GetVolumeList();
  while( v != "")
    class = GetVolumeClass( v);
    if ( class == "$ClassVolume" && v != "pwrs" && v != "pwrb" && v != "mps" && v != "NMps" && v != "TLog" && v != "SSAB" && v != "SA")

      if ( !file_open)
        file = fopen( fname, "w");
        if ( !file)
          printf("Unable to open file %s\n", fname);
          exit(0);
        endif

        fprintf( file, "printf(\"\\n\");\n");
        fprintf( file, "printf(\"-- %s\\n\");\n", fname);
        fprintf( file, "printf(\"-- Script to restore current classvolume versions\\n\\n\");\n");
        file_open = 1;
      endif

      attr = v + ":.RtVersion";
      version = GetAttribute( attr, sts);
      printf( "-- Store version %d of volume '%s'\n", version, v);
      fprintf(file, "printf( \"-- Restore version number %d in volume '%s'\\n\");\n", version, v);
      fprintf(file, "set volume/volume=%s\n", v);
      fprintf(file, "set attr/name=%s:/attr=RtVersion/value=%d/noconf\n", v, version);
      fprintf(file, "save\n");
    endif
    v = GetNextVolume(v);
  endwhile
  if ( file_open)
    fclose(file);
  endif

endmain
