!** Invisible: Load a backup dumpfile into development data base.
! 
# Proview   Open Source Process Control.
# Copyright (C) 2005-2014 SSAB EMEA AB.
#
# This file is part of Proview.
!
!  This program is free software; you can redistribute it and/or 
!  modify it under the terms of the GNU General Public License as 
!  published by the Free Software Foundation, either version 2 of 
!  the License, or (at your option) any later version.
!
!  This program is distributed in the hope that it will be useful 
!  but WITHOUT ANY WARRANTY; without even the implied warranty of 
!  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the 
!  GNU General Public License for more details.
!
!  You should have received a copy of the GNU General Public License 
# along with Proview. If not, see <http://www.gnu.org/licenses/>
#
# Linking Proview statically or dynamically with other modules is
# making a combined work based on Proview. Thus, the terms and 
# conditions of the GNU General Public License cover the whole 
# combination.
#
# In addition, as a special exception, the copyright holders of
# Proview give you permission to, from the build function in the
# Proview Configurator, combine Proview with modules generated by the
# Proview PLC Editor to a PLC program, regardless of the license
# terms of these modules. You may copy and distribute the resulting
# combined work under the terms of your choice, provided that every 
# copy of the combined work is accompanied by a complete copy of 
# the source code of Proview (the version used to produce the 
# combined work), being distributed under the terms of the GNU 
# General Public License plus this exception.
!
function int usage()
  printf( "Script to load backup dumpfile into development database.\n\n");
  printf( "pwrc -v 'volumename' @pwr_exe:restore_bck 'nodename' 'dumpfile'\n\n");
  printf( "Create a dumpfile on an process station with rt_bck -p\n\n");
endfunction

main()
  int dump_file;
  int alias_file;
  string str;
  string dbname;
  string volname;
  int i;
  int j;
  int first;
  int len;
  string start;
  string name;
  string objname;
  string attrname;
  string db_attrname;
  string value;
  string answer;
  
  int loaddb;
  int export_alias;
  string alias_filename;

  verify(0);  
  printf("\n");

  if (p1 == "" || p2 == "")
    usage();
    exit(0);
  endif

  dump_file = fopen(p2, "r");
  if (!dump_file)
    printf( "Unable to open dumpfile %s\n", p2);
    exit();
  endif

  loaddb = 0;
  ask("Do you want to load values from the dumpfile into the database? (Y/N) ", answer);
  if (answer == "y" || answer == "Y") 
    loaddb = 1;
  endif

  export_alias = 0;
  ask("Do you wan to create an alias-file with values from the dumpfile? (Y/N) ", answer);
  if (answer == "y" || answer == "Y") 
    ask("Alias-file name: ", answer);
    alias_file = fopen(answer, "w");
    if (!alias_file)
      printf( "Unable to open output alias file %s\n", answer);
      exit();
    endif
    export_alias = 1;
    alias_filename = answer;
  endif
    
  while (fgets(str, dump_file))
    start = extract(1, 2, str);
    i = strstr(start, "/");
    if (!i)
      j = strstr(str, " ");
      first = 1;
      len = j - 1;
      name = extract( first, len, str);	
      first = j + 1;
      len = strlen(str) - j;
      value = extract(first, len, str);
      i = strrchr(name, ".");
      first = 1;
      len = i - 1;
      objname = extract(first, len, name);
      first = i + 1;
      len = strlen(name) - i;
      attrname = extract(first, len, name);
  
      db_attrname = attrname;
      if (attrname == "ActualValue")
        db_attrname = "InitialValue";
      endif

      if (loaddb)
        set attr/attr='db_attrname'/value="'value'"/name='objname'/noconf/log
      endif
      
      if (export_alias)
        fprintf(alias_file, "%s_setvalp %s.%s = \"%s\"\n", p1, objname, attrname, value);
      endif
      
    endif
  endwhile
  
  if (dump_file)
    fclose(dump_file);
  endif
  
  if (alias_file)
    printf("Alias-file created, %s\n", alias_filename);
    fclose(alias_file);
  endif
  
  if (loaddb)
    ask("Do you wan to save the database changes? (Y/N) ", answer);
    if (answer == "y" || answer == "Y") 
      printf("Saving changes...\n");
      save
    else
      printf("Not saving any changes\n");
    endif
  endif

endmain







